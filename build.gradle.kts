plugins {
    id("java-library")
    id("idea")
	id("net.minecraftforge.accesstransformers") version "5.0.1"
	id("net.minecraftforge.gradle") version "7.0.0-beta.46"
	id("net.minecraftforge.jarjar") version "0.2.3"
}

val mod_name: String by project
val mod_id: String by project
val mod_group: String by project
val mod_version: String by project
val mod_author: String by project
val mod_description: String by project
val mod_license: String by project
val mod_credits: String by project

val java_version: String by project

val minecraft_version: String by project
val minecraft_version_range: String by project

val parchment_mappings: String? by project

val minecraftforge_version: String by project
val minecraftforge_version_range: String by project
val minecraftforge_eventbus_validator_version: String by project

version = mod_version

base {
    archivesName = mod_id
}

println(
	"Java: ${providers.systemProperty("java.version").get()}, " +
		"JVM: ${providers.systemProperty("java.vm.version").get()} (${
			providers.systemProperty("java.vendor").get()
		}), " +
		"Arch: ${providers.systemProperty("os.arch").get()}"
)

java {
    println("Java $java_version")
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    // withSourcesJar()
    // withJavadocJar()
}

minecraft {
	mappings(
		if (parchment_mappings != null) "parchment" else "official",
		if (parchment_mappings != null)
			"${minecraft_version}-${parchment_mappings}" else minecraft_version
	)

    setAccessTransformers(true)

	runs {
		configureEach {
			workingDir.convention(layout.projectDirectory.dir("run"))

			//systemProperty ("forge.logging.markers", "REGISTRIES")

			systemProperty("forge.logging.console.level", "debug")

			systemProperty("eventbus.api.strictRuntimeChecks", "true")

			//args ("-mixin.config=${mod_id}.mixins.json")

			//classpath(sourceSets.main.get())
		}

		register("client") {
			systemProperty("forge.enabledGameTestNamespaces", mod_id)
		}

		register("server") {
			systemProperty("forge.enabledGameTestNamespaces", mod_id)
			args("--nogui")
		}

		register("gameTestServer") {
			systemProperty("forge.enabledGameTestNamespaces", mod_id)
		}

		register("data") {
			workingDir = layout.projectDirectory.dir("run-data")

			args(
				"--mod",
				mod_id,
				"--all",
				"--output",
				layout.projectDirectory.dir("src/generated/resources"),
				"--existing",
				layout.projectDirectory.dir("src/main/resources")
			)
		}
	}
}

// Include resources generated by data generators.
sourceSets.main {
	resources.srcDir(layout.projectDirectory.dir("src/generated/resources"))
}

// This methods registers jarJar for the default jar task.
// The closure allows you to configure the task, instead of needing to do this:
jarJar.register() {
	archiveClassifier = null
}

repositories {
	maven(minecraft.mavenizer)
	maven(fg.forgeMaven)
	maven(fg.minecraftLibsMaven)
	exclusiveContent {
		forRepository {
			maven("https://repo.spongepowered.org/repository/maven-public") {
				name = "Sponge"
			}
		}
		filter {
			includeGroupAndSubgroups("org.spongepowered")
		}
	}
	mavenCentral()
	mavenLocal()
}

dependencies {
	// Specify the version of Minecraft to use.
	// Any artifact can be supplied so long as it has a "userdev" classifier artifact and is a compatible patcher artifact.
	// The "userdev" classifier will be requested and setup by ForgeGradle.
	// If the group id is "net.minecraft" and the artifact id is one of ["client", "server", "joined"],
	// then special handling is done to allow a setup of a vanilla dependency without the use of an external repository.
	implementation(minecraft.dependency("net.minecraftforge:forge:${minecraft_version}-${minecraftforge_version}"))

	// Forge 1.21.6+ uses EventBus 7, which shifts most of its runtime validation to compile-time via an annotation processor
	// to improve performance in production environments. This line is required to enable said compile-time validation
	// in your development environment, helping you catch issues early.
	annotationProcessor("net.minecraftforge:eventbus-validator:${minecraftforge_eventbus_validator_version}")

	// Example mod dependency with JEI
	// The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
	//compileOnly "mezz.jei:jei-${mc_version}-common-api:${jei_version}"
	//compileOnly "mezz.jei:jei-${mc_version}-forge-api:${jei_version}"
	//runtimeOnly "mezz.jei:jei-${mc_version}-forge:${jei_version}"

	// Example mod dependency using a mod jar from ./libs with a flat dir repository
	// This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
	// The group id is ignored when searching -- in this case, it is "blank"
	// NOTE: Support for deobfuscated dependencies has not yet been added in ForgeGradle 7.
	//implementation "blank:coolmod-${mc_version}:${coolmod_version}"

	// For more info:
	// http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
	// http://www.gradle.org/docs/current/userguide/dependency_management.html
}

tasks.withType(JavaCompile::class).configureEach {
	options.encoding = "UTF-8" // Use the UTF-8 charset for Java compilation
}

sourceSets.forEach {
	val dir = layout.buildDirectory.dir("sourcesSets/$it.name")
	it.output.setResourcesDir(dir)
	it.java.destinationDirectory = dir
}

tasks {
    processResources {
        val expandProps = mapOf(
            "java_version" to java_version,
            "version" to mod_version,
            "group" to mod_group,
            "mod_name" to mod_name,
            "mod_author" to mod_author,
            "mod_id" to mod_id,
            "license" to mod_license,
            "description" to mod_description,
            "credits" to mod_credits,
            "minecraft_version" to minecraft_version,
            "minecraft_version_range" to minecraft_version_range,
            "minecraftforge_version" to minecraftforge_version,
            "minecraftforge_version_range" to minecraftforge_version_range,
            "minecraftforge_eventbus_validator_version" to minecraftforge_eventbus_validator_version,
        ).filterValues { it.isNotEmpty() }.mapValues { (_, v) -> v }

        val jsonExpandProps = expandProps.mapValues { (_, v) -> v.replace("\n", "\\\\n") }

        filesMatching(listOf("META-INF/mods.toml")) {
            expand(expandProps)
        }

        filesMatching(listOf("pack.mcmeta", "*.mixins.json")) {
            expand(jsonExpandProps)
        }

        inputs.properties(expandProps)
    }
}